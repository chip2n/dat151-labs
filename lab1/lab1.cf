comment "//" ;
comment "/*" "*/" ;
comment "#" ;

-- programs
Prog. Program ::= [Def] ;

-- definitions
DQConst.  Def ::= "using" QConst ";" ;
DFunction. Def  ::= Type Id "(" [Arg] ")" Body;
DInlFunc.  Def  ::= "inline" Type Id "(" [Arg] ")" Body ;
DStat.    Def ::= Stm2 ;
separator Def "" ;

-- functions, function bodies and function arguments
Arg. Arg  ::= Type Id ;
Arg2. Arg ::= Type ;
Arg3. Arg ::= "const" Type ;
Arg4. Arg ::= "const" Type Id ;
Arg5. Arg ::= "const" Type Id "=" Exp;
ArgDef. Arg ::= Type Id "=" Exp ;
BStm. Body ::= "{" [Stm] "}" ;
BStmN. Body ::= ";" ;

Dec. Decl ::= Type [Var] ;
Varr.  Var ::= Id ;
separator nonempty Var "," ;
DecAss. Decl ::= Type Id "=" Exp ;
DecAss. Decl ::= "const" Type Id "=" Exp ;

D. Declaration ::= Type [Var] ";" ;
terminator Stm "" ;
separator Decl "," ;
separator nonempty Id "," ;
separator nonempty String "" ;
separator Arg "," ;
separator Exp "," ;
separator nonempty Type "," ;
separator Declaration "" ;

STypeDef.  Stm2  ::= "typedef" Type Id ";" ;
SDecl.     Stm2  ::= Decl ";" ;
SStruct.   Stm2  ::= "struct" Id "{" [Declaration] "}" ";" ;
SExp.      Stm  ::= Exp ";" ;
SEmpty.    Stm  ::= ";" ;
SQual.     Stm  ::= "using" QConst ";" ;
SReturn.   Stm  ::= "return" Exp ";" ;
SWhile.    Stm  ::= "while" "(" Exp ")" Stm ;
SDoWhile.  Stm  ::= "do" Stm "while" "(" Exp ")" ";" ;
SBlock.    Stm  ::= "{" [Stm] "}" ;
SIf.       Stm  ::= "if" "(" Exp ")" Stm ;
SIfElse.   Stm  ::= "if" "(" Exp ")" Stm "else" Stm ;
SFor.      Stm  ::= "for" "(" Decl ";" Exp ";" Exp ")" Stm ;

coercions Stm 2 ;

EInt.     Exp16  ::= Integer ;
EDouble.  Exp16  ::= Double ;
EChar.    Exp16  ::= Char ;
EString.  Exp16  ::= [String] ;
ETrue.    Exp16  ::= "true";
EFalse.   Exp16  ::= "false";
EConst.   Exp15  ::= QConst ;
EIndex.   Exp15  ::= Id "[" Exp "]" ;
ECall.    Exp15  ::= Id "(" [Exp] ")" ;
EMem.     Exp14  ::= Exp14 "." Exp15 ;
EFAccs.   Exp14  ::= Exp14 "->" Exp15 ;
EPostInc. Exp14  ::= Exp15 "++" ;
EPostDec. Exp14  ::= Exp15 "--" ;
EDeref.   Exp14  ::= "*" Exp15 ;
EPreInc.  Exp13  ::= "++" Exp14 ;
EPreDex.  Exp13  ::= "--" Exp14 ;
ENot.     Exp13  ::= "!" Exp14 ;
ENeg.     Exp13  ::= "-" Exp14 ;
EMul.     Exp12  ::= Exp12 "*" Exp13 ;
EDiv.     Exp12  ::= Exp12 "/" Exp13 ;
EMod.     Exp12  ::= Exp12 "%" Exp13 ;
EAdd.     Exp11  ::= Exp11 "+" Exp12 ;
ESub.     Exp11  ::= Exp11 "-" Exp12 ;
EShiftL.  Exp10  ::= Exp10 "<<" Exp11 ;
EShiftR.  Exp10  ::= Exp10 ">>" Exp11 ;
EGt.      Exp9   ::= Exp9 ">" Exp10 ;
ELt.      Exp9   ::= Exp9 "<" Exp10 ;
ELeq.     Exp9   ::= Exp9 "<=" Exp10 ;
EGeq.     Exp9   ::= Exp9 ">=" Exp10 ;
ENeq.     Exp8   ::= Exp8 "!=" Exp9 ;
EEq.      Exp8   ::= Exp8 "==" Exp9 ;
EAnd.     Exp4   ::= Exp4 "&&" Exp5 ;
EOr.      Exp3   ::= Exp3 "||" Exp4 ;
EAss.     Exp2   ::= Exp3 "=" Exp2;
EAssDec.  Exp2   ::= Exp3 "-=" Exp2 ;
EAssInc.  Exp2   ::= Exp3 "+=" Exp2 ;
EIfElse.  Exp2   ::= Exp3 "?" Exp2 ":" Exp2 ;
EThrow.   Exp1   ::= "throw" Exp2 ;

coercions Exp 16;

TInt.     Type ::= "int" ;
TDouble.  Type ::= "double" ;
TVoid.    Type ::= "void" ;
TBool.    Type ::= "bool" ;
TConst.   Type ::= QConst ;
TRef.     Type ::= Type "&" ;

QCon. QConst ::= [Const] ;
CId. Const ::= Id ;
CTemp. Const ::= Id "<" [Type] ">" ;
separator nonempty  Const "::" ;

token Id (letter (letter | digit | '_')*) ;
