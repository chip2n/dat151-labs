comment "//" ;
comment "/*" "*/" ;
comment "#" ;

-- programs
Prog. Program ::= [Def] ;

-- definitions
DQConst.  Def ::= "using" QConst ";" ;
DStat.    Def ::= Stm2 ;
separator Def "" ;

-- functions, function bodies and function arguments
Fun. Function ::= Type Id "(" [Arg] ")" ;
Arg. Arg  ::= Type Id ;
Arg2. Arg ::= Type ;
ArgDef. Arg ::= Type Id "=" Exp ;
BStm. Body ::= "{" [Stm] "}" ;
BStmN. Body ::= ";" ;

Dec. Decl ::= Type [Id] ;
DecAss. Decl ::= Type Id "=" Exp ;
Qual. Qualifier ::= "using" QConst "::" QConst ;

D. Declaration ::= Type [Id] ";" ;
terminator Stm "" ;
separator Decl "," ;
separator nonempty Id "," ;
separator nonempty String "" ;
separator Arg "," ;
separator Exp "," ;
separator nonempty Type "," ;
separator Declaration "" ;

STypeDef.  Stm2  ::= "typedef" Type Id ";" ;
SDecl.     Stm2  ::= Decl ";" ;
SAss.      Stm2  ::= Decl "=" Exp ";" ;
SStruct.   Stm2  ::= "struct" Id "{" [Declaration] "}" ";" ;
SExp.      Stm  ::= Exp ";" ;
SEmpty.    Stm  ::= ";" ;
SQual.     Stm  ::= Qualifier ";" ;
SReturn.   Stm  ::= "return" Exp ";" ;
SFunction. Stm2  ::= Function Body;
SInlFunc.  Stm2  ::= "inline" Function Body ;
SWhile.    Stm  ::= "while" "(" Exp ")" Stm ;
SDoWhile.  Stm  ::= "do" Stm "while" "(" Exp ")" ";" ;
SBlock.    Stm  ::= "{" [Stm] "}" ;
SIf.       Stm  ::= "if" "(" Exp ")" Stm ;
SIfElse.   Stm  ::= "if" "(" Exp ")" Stm "else" Stm ;
SFor.      Stm  ::= "for" "(" Decl ";" Exp ";" Exp ")" Stm ;

coercions Stm 2 ;

EInt.     Exp16  ::= Integer ;
EDouble.  Exp16  ::= Double ;
EChar.    Exp16  ::= Char ;
EString.  Exp16  ::= [String] ;
ETrue.    Exp16  ::= "true";
EFalse.   Exp16  ::= "false";
EConst.   Exp15  ::= QConst ;
EIndex.   Exp15  ::= Id "[" Exp "]" ;
ECall.    Exp15  ::= Id "(" [Exp] ")" ;
EMem.     Exp14  ::= Exp14 "." Exp15 ;
EFAccs.   Exp14  ::= Exp14 "->" Exp15 ;
EPostInc. Exp14  ::= Exp15 "++" ;
EPostDec. Exp14  ::= Exp15 "--" ;
EDeref.   Exp14  ::= "*" Exp15 ;
EPreInc.  Exp13  ::= "++" Exp14 ;
EPreDex.  Exp13  ::= "--" Exp14 ;
ENot.     Exp13  ::= "!" Exp14 ;
ENeg.     Exp13  ::= "-" Exp14 ;
EMul.     Exp12  ::= Exp12 "*" Exp13 ;
EDiv.     Exp12  ::= Exp12 "/" Exp13 ;
EMod.     Exp12  ::= Exp12 "%" Exp13 ;
EAdd.     Exp11  ::= Exp11 "+" Exp12 ;
ESub.     Exp11  ::= Exp11 "-" Exp12 ;
EShiftL.  Exp10  ::= Exp10 "<<" Exp11 ;
EShiftR.  Exp10  ::= Exp10 ">>" Exp11 ;
EGt.      Exp9   ::= Exp9 ">" Exp10 ;
ELt.      Exp9   ::= Exp9 "<" Exp10 ;
ELeq.     Exp9   ::= Exp9 "<=" Exp10 ;
EGeq.     Exp9   ::= Exp9 ">=" Exp10 ;
ENeq.     Exp8   ::= Exp8 "!=" Exp9 ;
EEq.      Exp8   ::= Exp8 "==" Exp9 ;
EAnd.     Exp4   ::= Exp4 "&&" Exp5 ;
EOr.      Exp3   ::= Exp3 "||" Exp4 ;
EAss.     Exp2   ::= Exp3 "=" Exp2;
EAssDec.  Exp2   ::= Exp3 "-=" Exp2 ;
EAssInc.  Exp2   ::= Exp3 "+=" Exp2 ;
EIfElse.  Exp2   ::= Exp3 "?" Exp2 ":" Exp2 ;
EThrow.   Exp1   ::= "throw" Exp2 ;

coercions Exp 16;

TInt.     Type2 ::= "int" ;
TDouble.  Type2 ::= "double" ;
TVoid.    Type2 ::= "void" ;
TBool.    Type2 ::= "bool" ;
TConst.   Type2 ::= QConst ;
TRef.     Type2 ::= Type2 "&" ;
TConstOp. Type ::= "const" Type2 ;

coercions Type 2;

QCon. QConst ::= [Const] ;
CId. Const ::= Id ;
CTemp. Const ::= Id "<" [Type] ">" ;
separator nonempty  Const "::" ;

token Id (letter (letter | digit | '_')*) ;
